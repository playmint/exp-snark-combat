// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/utils/Address.sol";
import "@openzeppelin/contracts/utils/StorageSlot.sol";



contract Proxy
{
    bytes32 private constant _IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;
    bytes32 private constant _ADMIN_SLOT = 0xb53127684a568b3173ae13b9f8a6016e243e63b6e8ee1178d6a717850b5d6103;

    constructor(address implementation, bytes memory data) payable
    {
        StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value = implementation;
        StorageSlot.getAddressSlot(_ADMIN_SLOT).value = msg.sender;

        if (data.length > 0)
        {
            Address.functionDelegateCall(implementation, data, /*errorMessage*/ "init failed");
        }
    }

    fallback() external payable
    {
        _fallback();
    }

    receive() external payable 
    {
        _fallback();
    }

    function _fallback() private
    {
        address implementation = StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value;

        // from OpenZeppelin/contracts
        assembly 
        {
            // Copy msg.data. We take full control of memory in this inline assembly
            // block because it will not return to Solidity code. We overwrite the
            // Solidity scratch pad at memory position 0.
            calldatacopy(0, 0, calldatasize())

            // Call the implementation.
            // out and outsize are 0 because we don't know the size yet.
            let result := delegatecall(gas(), implementation, 0, calldatasize(), 0, 0)

            // Copy the returned data.
            returndatacopy(0, 0, returndatasize())

            switch result
            // delegatecall returns 0 on error.
            case 0 {
                revert(0, returndatasize())
            }
            default {
                return(0, returndatasize())
            }
        }
    }
}